= Actors and async data
:proglang: Motoko
:company-id: DFINITY

The programming model of the {IC} consists of memory-isolated canisters communicating by asynchronous message passing of unstructured, binary data. A canister processes its message one-at-a-time, preventing race conditions.  A canister uses call-backs to register what needs to be done with the result of any inter-canister messages it issues.

{proglang} abstracts the complexity of the {IC} with a well known, higher-level abstraction: the _actor model_.
Each canister is represented as a typed actor. The type of an actor lists the messages
it can handle. Each message is abstracted as a typed, asynchronous function, imposing structure on the raw binary data of the underlying {IC}.
An actor is similar to an object, but is different in that its state is completely isolated,
its interactions with the world are entirely through asynchronous, messaging, and its messages are processed one-at-a-time, even when issued in parallel by concurrent actors.

In {proglang}, sending a message to an actor is a function call, but instead of
blocking the caller until the call has returned, the message is enqueued
on the callee, and a promise representing that unfulfilled request
immediately returned to the caller.  The promise is a placeholder for the eventual result
of the request, that the caller can later query. Between issuing the
request, and deciding to wait for the result, the caller is free to do
other work, including issuing more requests to the same or other
actors.
Once the caller has processed the request, the promise is fulfilled and its result made
available to the callee.
If the callee is waiting on the promise, its execution can resume with the result, otherwise the result is simply stored in the promise for future examination.

In {proglang}, actors have dedicated syntax and actor types; messaging is handled by so called _shared_ functions returning promises (shared because they are available to remote actors);
a promise, `p`, is a value of the special type `async T` for some type `T`; waiting
on `p` to be fulfilled is expressed using `await p` to obtain a value of type `T`.
To avoid introducing shared state through messaging, e.g. by sending an object or mutable array,
the data that can be transmitted through shared functions is restricted to immutable, _shared_ types.


////
TODO: DELETE

{proglang} provides an *actor-based* programming model to developers to express _server behavior_, including that of _micro services_ on the Internet and the Internet Computer.

An actor is similar to an object, but is special in that its isolated state exists _remotely_, and its interactions with the world are _asynchronous_.

Each {proglang} actor represents a service that one might want to deploy on the Internet Computer.


The interface of each actor introduces `async` data whenever it returns information to its caller.
This programming abstraction serves a key role in {proglang}, as it coordinates with the transformations of the {proglang} compiler pipeline and eventual execution behavior of {proglang} actors on the Internet Computer.

This abstraction represents a _promise_ from the system to the caller, on behalf of the callee:

 - Either the `async` value, when ``await``ed, will yield a value from the callee of the expected type,

 - or, an error --- system-level or callee-level --- will eventually arise.

In general, the caller may not _immediately_ `await` each call.  But even in cases when they do, they use the same `async` and `await` abstractions, for the same reason: to maintain the illusion of call-return, direct-style control flow, as supported by the {proglang} compiler's transformations.

*Technical aside.* In reality, the underlying message-passing of the system forces the program's logic into another form.
Specifically, control flow around each actor method call involves the program loosing control to a system-level message-processing loop, which forces the program's logic into a so-called
_"continuation-passing-style"_ (CPS) to expose event-handling
_"callback functions"_.
This program structure is complex for humans to read and maintain, and stands in stark contrast to the direct style most prefer for most program logic.

We note that {proglang} programs may avoid callbacks for many cases, but not _all_ cases where they are used in other asynchronous, message-passing settings.
Notably, callbacks are still needed when they serve as a fundamental aspect of the service's interface, as with a link:sharing{outfilesuffix}[publish-subscribe] service, where users register with the service to get notified some times later, when some predetermined class of events, occur over time.
////

To start, we consider the simplest stateful service: a counter actor, the remote counterpart of our previous, local counter object.


== Example: a Counter service

Consider the following actor declaration:

[source,motoko]
----
include::../examples/counter-actor.mo[]
----

////
actor counter {

  var count = 0;

  public shared func inc() : async () { count += 1 };

  public shared func read() : async Nat { count };

  public shared func bump() : async Nat {
    count += 1;
    count;
  };
};
////

The `counter` actor declares one field and three public, _shared_ functions:

* the field `count` is mutable, initialized to zero and implicitly `private`.
* function `inc()` asynchronous increments the counter and returns a promise of type `async ()` for synchronization.
* function `read()` asynchronously reads the counter value and returns a promise of type `async Nat` containing its value.
* function `bump()` asynchronously increments and reads the counter.

Shared functions, unlike local functions, are accessible to remote callers and have additional restrictions: their arguments and return value must be _shared_ types - a subset of types that includes immutable data, actor references and shared function references, but excludes functions,
values containing functions and mutable data. Because all interaction with actors is asynchronous,
an actor's functions must either return a promise type `async T`, for some type `T`, or nothing at all `()`.

The only way to read or modify the state (`count`) of the `counter` actor is through its shared functions.

A value of type `async T` is a _promise_. The producer of the promise
fulfills the promise when it returns a result, either a value or error.

Unlike objects and modules, actors can only expose shared functions. For this reason, {proglang} allows you to omit the `shared` modifier on public actor functions, allowing the more concise, but equivalent, actor declaration:

[source,motoko]
----
include::../examples/counter-actor-sugar.mo[]
----

For now, the only place shared functions can be declared is in the
body of an actor or actor class.  Despite this restriction, shared
functions are still first-class values in {proglang} and can be passed
as arguments or results, and stored in data structures.

The type of a shared functions is specified using a shared function type. For example, the
value `inc` has type `shared () -> async Nat` and could be supplied
as a standalone callback to some other service (see link:sharing{outfilesuffix}[publish-subscribe] for an example).

== Actor types

Just as objects have object types, actors have _actor types_. The
`counter` actor has the following type:

....
actor {
  inc  : shared () -> async ();
  read : shared () -> async Nat;
  bump : shared () -> Nat;
}
....

Again, because the `shared` modifier is required on an actor's function members,
{proglang} both elides them on display, and allows you to omit them
when authoring an actor type.

Thus the previous type can be expressed more succinctly as:

....
actor {
  inc  : () -> async ();
  read : () -> async Nat;
  bump : () -> Nat;
}
....

Like object types, actor types support subtyping: an actor type is a subtype of a more general one that offers fewer functions with more general types.

== Using `await` to consume async promises

The caller of a shared function typically receives a promise, a value of type `async T` for some T.

The only thing the caller, a consumer, can do with this promise is
wait for it to be fulfilled by the producer, throw it away, or store
it for later use.

To access the result of an `async` value, the receiver of the promise use an  `*await*` expression.

For example, to use the result of `Counter.read()` above, we can first bind the
promise to an identifier `a`, and then `await a` to retrieve the underlying `Nat`, `n`:

[source, motoko]
----
let a : async Nat = counter.read();
let n : Nat = await a;
----

The first line immediately receives _a promise of the current value_, but does not wait for it, and thus cannot (yet) use it as a natural number.

The second line ``await``s this promise and extracts the result, a natural number.
This line may suspend execution until the promise has been fulfilled.

Typically, one rolls the two steps into one and one just awaits an asynchronous call directly:

[source, motoko]
----
let n : Nat = await counter.read();
----

Unlike a local function call, which blocks the caller until the callee has returned a result,
a shared function call immediately returns a promise, `p`, without blocking.
Instead of blocking, a later call to `await p` can suspend the current computation until or unless `p` is fulfilled.
Once the promise if fulfilled (by the producer), execution of `await p` resumes with its result.
If the result is a value, `await p` returns that value.
Otherwise the result is some error, and `await p` propagates the error to the
consumer of `await p`.

Awaiting a promise a second time will just produce the same result.

WARNING: A function that does not `await` in its body is guaranteed to
execute atomically - in particular, the environment cannot change the
state of the actor while the function is executing.  If a function
performs an `await`, however, atomicity is no longer guaranteed.  Between suspension
and resumption around the `await`, the state of the enclosing actor
may change due to concurrent processing of other incoming actor
messages. It is the programmer's responsibility to guard against
non-synchronized state changes.

For example, the implementation of `bump()` above is guaranteed to increment and read the
value of `count`, in one atomic step. The alternative implementation:

....
  public shared func bump() : async Nat {
    await inc();
    await read();
  };
....

does _not_ have the same semantics and allows another client of the actor to
interfere with its operation: each `await` suspends execution, allowing an interloper
to change the state of the actor.
By design, the explicit ``await``s make the potential points of interference clear to the reader.

== Query functions

In {IC} terminology, all three `counter` functions are _update_ messages
than can alter the state of the canister when called. Effecting a state change requires
agreement amongst the distributed replicas before the {IC} can
commit the change and return a result.  Reaching consensus is an
expensive process with relatively high latency.

For applications that don't
require the guarantees of consensus, the {IC} supports more efficient
_query_ operations. These are able to read the state of a canister from a single replica,
modify a snapshot during their execution and return a result, but
cannot permanently alter the state or send further {IC} messages.

{proglang} supports the implementation of {IC} queries using `query` functions. The `query` keyword
modifies the declaration of a (shared) actor function so that it executes with non-committing, and faster,
{IC} query semantics.

For example, we can extend the `counter` actor with fast-and-loose variant of the trustworthy `read` function, called `peek`:

[source,motoko]
----
include::../examples/CounterWithQuery.mo[]
----

The `peek()` function might be used by a `counter` frontend offering a quick, but less trustworthy,
display of the current counter value.

It is a compile-time error for a query method to call an actor function since this would violate dynamic restrictions imposed by the {IC}. Calls to ordinary functions are permitted.

Query functions can be called from non-query functions. Because those nested calls require consensus,
the efficiency gains of nested query calls will be modest at best.

The `query` modifier is reflected in the type of a query function:

....
  peek : shared query () -> async Nat
....

As before, in `query` declarations and actor types the `shared` keyword can be omitted.

== Messaging Restrictions

The {IC} places restrictions on when and how canisters are allowed to communicate. These restrictions are enforced dynamically on the {IC} but prevented statically in {proglang}, ruling out a class
of dynamic execution errors. Two examples are:

*  canister installation can execute code, but not send messages.
*  a canister query method cannot send messages.

These restrictions are surfaced in {proglang} as a restrictions on the context in which a shared function can be called, a promise can be awaited and messaging errors can be ``throw``n or handled via `try`/`catch`.

In {proglang} calling a shared function is an error unless the function is called from an async expression, the body of a (non-query) shared function, or the body of a local function with an async return type. Calling a shared function from actor class constructor is also an error.

Similarly, the `await` construct is only allowed in an asynchronous context: the body of an async expression the body of a shared function, or the body of local function with `async` return type.

It is only possible to `throw` or `try/catch` errors in an asynchronous context, since structured error handling is supported for messaging only.

These restrictions also mean that local functions cannot directly call shared functions or `await` promises. This limitation is awkward in some scenarios:  we hope to extend the type system to be more permissive in future.

////
TODO: scoped awaits (if at all)
////


[#actor_classes]
== Actor classes generalize actors

An actor _class_ generalizes a single actor declaration to the declaration of family of actors satisfying the same interface.
An actor class declares a type, naming the interface of its actors, and a function that
constructs a fresh actor of that type each time it is supplied with an argument.
An actor class thus serves as a template for manufacturing actors.
Because canister installation is asynchronous on the {IC}, the constructor function is asynchronous too, and returns its actor in a promise.

For example, we can generalize `counter` given above to `Counter(init)`
below, by introducing a constructor parameter, variable `init` of type `Nat`:

.Counters.mo
[source, motoko]
----
include::../examples/Counters.mo[]
----

If this is class is stored in file `+Counters.ml+`, then
we can import the file as a module and use it to
create several actors with different initial values:

[source, motoko]
----
import Counters "Counters";

let c1 = await Counters.Counter(1);
let c2 = await Counters.Counter(2);
----

The last two lines above _instantiate_ the actor class twice.
The first invocation uses the initial value `1`, where the second uses initial value `2`.
Because actor class instantiation is asynchronous, each call to `Counter(init)` returns a promise that can be ``await``ed for the resulting actor value.
Both `c1` and `c2` have the same type, `Counters.Counter` and can be used interchangeably.


NOTE: For now, the {proglang} compiler gives an error when compiling
programs that do not consist of a single actor or actor class.
Compiled programs may still, however, reference imported actor classes.
For more information, see link:modules-and-imports{outfilesuffix}#importing_actor_classes[Importing actor classes] and link:actor-classes{outfilesuffix}#actor_classes[Actor classes].

